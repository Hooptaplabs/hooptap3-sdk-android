apply plugin: 'android-library'
apply plugin: 'maven'
apply from: '../jacoco.gradle'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
        defaultPublishConfig "myVariant"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled = true
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

}


uploadArchives {
    repositories {
        mavenDeployer {
            mavenLocal()
        }
    }
}

task jarTask(type: Jar) {
    baseName = "my-sdk-android"
    from 'src/main/java'
}

task createJarWithDependencies(type: Jar) {
    baseName = "my-sdk-android-jar-with-dependencies"

    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }

    }

    with jarTask
}

configurations {
    jarConfiguration
}

artifacts {
    jarConfiguration jarTask
}

// Master task that will publish all variants
def DefaultTask masterTask = project.tasks.create("installArchives", DefaultTask)

android.libraryVariants.all { variant ->
    variant.outputs.each { output ->

        // Configuration defines which artifacts will be published, create one configuration for each variant output (artifact)
        def Configuration variantConfiguration = project.configurations.create("${variant.name}Archives")
        project.artifacts.add(variantConfiguration.name, output.packageLibrary)

        // Create one Upload type task for each configuration
        def Upload variantTask = project.tasks.create("${variant.name}Install", Upload)
        variantTask.configuration = variantConfiguration
        variantTask.repositories.mavenInstaller {
            pom.groupId = "com.hooptap.awssdk"
            pom.artifactId = "HooptapAwsSDK"
            pom.version = "1.0.0" //Give a different version for each variant
            pom.packaging = "aar"
        }

        // Make all tasks depend on each other and on master task
        masterTask.dependsOn variantTask
        masterTask = variantTask
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.google.android.gms:play-services-location:7.5.0'
    androidTestCompile 'org.unitils:unitils-core:3.3'


    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.4'
    androidTestCompile 'com.android.support.test:rules:0.4'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile "org.mockito:mockito-core:1.+"
    androidTestCompile "com.google.dexmaker:dexmaker:1.2"
    androidTestCompile "com.google.dexmaker:dexmaker-mockito:1.2"

    compile 'com.android.support.test:runner:0.4'
}


